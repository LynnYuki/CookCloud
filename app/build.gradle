apply plugin: 'com.android.application'
apply plugin: 'bugly'
bugly {
    appId = "2d8e531cd1" // 注册时分配的App ID
    appKey = "23fcc241-446f-49a7-bb55-dd984688b372" // 注册时分配的App Key
}

def versionMajor = 1
def versionMinor = 2
def versionPatch = 1
def versionBuild = 0

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}


android {
    compileSdkVersion 24
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.example.administrator.cookman"
        minSdkVersion 18
        targetSdkVersion 24
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi' , 'armeabi-v7a', 'arm64-v8a'//, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }

    }

    buildTypes {
        debug {
        }

        release {
            //移除无用的资源文件
            shrinkResources true
            zipAlignEnabled true

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //输出APK路径
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为厨客_v1.0_2015-01-15_渠道.apk
                        def fileName = "厨客_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }

        }
    }

    //多渠道打包
    productFlavors {
        ATest{}
        Fir{}
        Tencent {}
        Qh360 {}
        WangDouJia {}
        XiaoMi {}
        BaiDu {}
        Huawei {}
    }

    //多渠道打包
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [APK_CHANNEL_VALUE: name]
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:support-v13:24.2.1'
    compile 'com.android.support:design:24.2.1'

    // RxJava
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'

    //retrofit
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta4'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'

    // 数据处理
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'

    // 数据库
    compile 'org.litepal.android:core:1.3.2'

    //图像缓存
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // 依赖注入
    compile 'com.jakewharton:butterknife:7.0.1'

    //友盟统计
    compile 'com.umeng.analytics:analytics:latest.integration'
    //腾讯Bugly
    compile 'com.tencent.bugly:crashreport:latest.release'
    compile 'com.tencent.bugly:nativecrashreport:latest.release'

    compile 'com.wang.avi:library:1.0.1'
    compile 'de.hdodenhof:circleimageview:1.3.0'

    // 第三方架包
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-codec:commons-codec:1.10'

    testCompile 'junit:junit:4.12'
}